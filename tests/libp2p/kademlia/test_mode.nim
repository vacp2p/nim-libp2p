# SPDX-License-Identifier: Apache-2.0 OR MIT
# Copyright (c) Status Research & Development GmbH

{.used.}

import chronos
import ../../../libp2p/[builders, protocols/kademlia, switch]
import ../../../libp2p/protocols/connectivity/autonat/types
import ../../tools/[lifecycle, unittest]
import ./utils

proc dialFindNode(
    querier, target: KadDHT
): Future[Opt[Message]] {.async: (raises: [CancelledError]).} =
  ## Dial the target's Kad codec, send a FIND_NODE and return the reply, or
  ## `Opt.none` when the target refuses to serve (stream reset, no reply).
  let stream =
    try:
      await querier.switch.dial(
        target.switch.peerInfo.peerId, target.switch.peerInfo.addrs, querier.codec
      )
    except CatchableError:
      return Opt.none(Message)
  defer:
    await stream.close()

  let msg = Message(
    msgType: Opt.some(MessageType.findNode), key: Opt.some(target.rtable.selfId)
  )
  try:
    await stream.writeLp(msg.encode(querier.config.hideConnectionStatus))
    let replyBuf = await stream.readLp(MaxMsgSize).wait(1.seconds)
    let reply = Message.decode(replyBuf).valueOr:
      return Opt.none(Message)
    # A served query yields a well-formed FIND_NODE reply; anything else is a
    # broken server masquerading as one, so do not count it as served.
    if reply.msgType != Opt.some(MessageType.findNode):
      return Opt.none(Message)
    return Opt.some(reply)
  except CatchableError:
    return Opt.none(Message)

suite "KadDHT dynamic mode":
  teardown:
    checkTrackers()

  test "initial mode follows the configured flag":
    check:
      setupKad(isServer = true).isServer
      not setupKad(isServer = false).isServer

  asyncTest "changeMode reports whether the mode changed":
    let kad = setupKad(isServer = false)

    check (await kad.changeMode(isServer = true))
    check kad.isServer

    # The same mode twice changes nothing.
    check not (await kad.changeMode(isServer = true))
    check kad.isServer

    check (await kad.changeMode(isServer = false))
    check not kad.isServer

  asyncTest "the reachability handler drives the mode":
    let kad = setupKad(isServer = false)
    let onReachability = kadReachabilityHandler(kad)

    template notify(reachability: NetworkReachability): untyped =
      await onReachability(reachability, Opt.none(float), Opt.none(MultiAddress))

    notify(NetworkReachability.Unknown)
    check not kad.isServer # no verdict yet, so the mode stays untouched

    notify(NetworkReachability.Reachable)
    check kad.isServer

    notify(NetworkReachability.Unknown)
    check kad.isServer

    notify(NetworkReachability.NotReachable)
    check not kad.isServer

  asyncTest "server answers queries, client resets them":
    let querier = setupKad()
    let server = setupKad(isServer = true)
    let client = setupKad(isServer = false)
    startAndDeferStop(@[querier, server, client])

    check (await querier.dialFindNode(server)).isSome()
    check (await querier.dialFindNode(client)).isNone()

  asyncTest "downgrade to client mode stops serving and drops peers":
    let querier = setupKad()
    let server = setupKad(isServer = true)
    startAndDeferStop(@[querier, server])

    check (await querier.dialFindNode(server)).isSome()

    check (await server.changeMode(isServer = false))
    check (await querier.dialFindNode(server)).isNone()

    check (await server.changeMode(isServer = true))
    check (await querier.dialFindNode(server)).isSome()

  asyncTest "downgrade resets an in-flight inbound stream":
    let querier = setupKad()
    let server = setupKad(isServer = true)
    startAndDeferStop(@[querier, server])

    let stream = await querier.switch.dial(
      server.switch.peerInfo.peerId, server.switch.peerInfo.addrs, querier.codec
    )
    defer:
      await stream.close()

    # One round-trip parks the server handler on the next read, so the stream is
    # held open and tracked as a server stream.
    let msg = Message(
      msgType: Opt.some(MessageType.findNode), key: Opt.some(server.rtable.selfId)
    )
    await stream.writeLp(msg.encode(querier.config.hideConnectionStatus))
    discard await stream.readLp(MaxMsgSize).wait(1.seconds)

    checkUntilTimeout:
      server.serverStreams.len == 1

    check (await server.changeMode(isServer = false))
    check server.serverStreams.len == 0

    # The downgrade must reset the parked stream: the querier's next read ends in
    # EOF, not a timeout (a timeout would mean the reset never propagated).
    expect LPStreamEOFError:
      discard await stream.readLp(MaxMsgSize).wait(3.seconds)
